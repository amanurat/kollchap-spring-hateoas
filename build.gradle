buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE'
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'spring-boot'

repositories {
	mavenLocal()
	maven { url 'https://repo.spring.io/snapshot' }
	mavenCentral()
}

group = 'kollchap'

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.boot:spring-boot-starter-hateoas'
	compile 'org.springframework.boot:spring-boot-starter-data-rest'

	runtime 'com.h2database:h2'
	runtime 'org.atteo:evo-inflector:1.2'

	testCompile 'com.jayway.jsonpath:json-path'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.restdocs:spring-restdocs-core:1.1.0.RELEASE'
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.0.RELEASE'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
	outputs.dir snippetsDir
}

asciidoctor {
	sourceDir 'src/main/asciidoc' // Align with Maven's default location
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}